USE Pitson
GO

--===============================================================================
-- Procedure qui permet de créer un nouveau modèle dans la base
-- @idModele est le nom du modèle à créer
-- @diametre est le diamètre nominal du modèle
-- @message est le message retour
-- Code retour :
--			0. OK
--			1. Paramètre d'entrée à valeur nulle ou incohérente
--			2. Identifiant modèle existant
--			3. Exception
--===============================================================================
CREATE PROC sp_creerModele @idModele TypeIDModele, @diametre TypeMesure,  @messageRetour TypeMessageRetour output
AS
DECLARE @codeRetour Tinyint;

BEGIN TRY
	BEGIN
		IF @idModele IS NULL OR @idModele = ''
		BEGIN
			SET @codeRetour = 1 ;
			SET @messageRetour = 'Nom du modèle nul ou vide.';
		END
		ELSE IF @diametre IS NULL OR @diametre <= 0
		BEGIN
			SET @codeRetour = 1 ;
			SET @messageRetour = 'Le diamètre renseigné est nul ou négatif.';
		END
		ELSE IF EXISTS (SELECT idModele FROM Modele WHERE idModele = @idModele)
		BEGIN
			SET @codeRetour = 2 ;
			SET @messageRetour = 'Nom du modèle déjà existant.';
		END
		ELSE
		BEGIN
		-- Par défaut lors de la création du modèle, le BIT Obsolete vaut 0
			INSERT INTO Modele VALUES (@idModele, @diametre, 0);
			INSERT INTO Stock VALUES(@idModele, 'Petit', 0, 4);
			INSERT INTO Stock VALUES(@idModele, 'Moyen', 0, 4);
			INSERT INTO Stock VALUES(@idModele, 'Grand', 0, 4);
			SET @codeRetour = 0 ;
			SET @messageRetour = 'Modèle ajouté dans la table avec succès.';
		END
	END
END TRY
BEGIN CATCH
	SET @messageRetour = 'Erreur base de données : ' + ERROR_MESSAGE() ;
	SET @codeRetour = 3 ;
END CATCH

RETURN @codeRetour;
GO


--===============================================================================
-- Procedure qui permet de supprimer un modèle dans la base
-- @idModele est le nom du modèle à supprimer
-- @messageRetour est le message retour
-- Code retour :
--			0. OK
--			1. Paramètre d'entrée à valeur nulle ou incohérente
--			2. Identifiant modèle non reconnu
--			3. Modèle encore lié à une autre table
--			4. Exception
--===============================================================================
CREATE PROC sp_supprimerModele @idModele TypeIDModele, @messageRetour TypeMessageRetour output
AS
DECLARE @codeRetour Tinyint;

BEGIN TRY
	BEGIN
		IF @idModele IS NULL OR @idModele = ''
		BEGIN
			SET @codeRetour = 1 ;
			SET @messageRetour = 'Nom du modèle nul ou vide.';
		END
		ELSE IF NOT EXISTS (SELECT idModele FROM Modele WHERE idModele = @idModele)
		BEGIN
			SET @codeRetour = 2 ;
			SET @messageRetour = 'Ce modèle n''existe pas dans la table.';
		END
		ELSE IF EXISTS (SELECT idModele FROM Lot WHERE idModele = @idModele)
		BEGIN
			SET @codeRetour = 3 ;
			SET @messageRetour = 'Ce modèle est encore lié à des lots existants.';
		END
		ELSE IF (SELECT SUM (qtStock) FROM Stock WHERE idModele = @idModele GROUP BY idModele) <> 0
		BEGIN
			SET @codeRetour = 3 ;
			SET @messageRetour = 'Ce modèle est encore lié à des pièces en stock.';
		END
		ELSE
		BEGIN
			-- Suppression du modèle dans la table stock
			DELETE Stock
			FROM Stock
			WHERE idModele = @idModele ;
			-- Suppression du modèle dans la table modèle
			DELETE Modele
			FROM Modele
			WHERE idModele = @idModele ;
			SET @codeRetour = 0 ;
			SET @messageRetour = 'Modèle supprimé de la table avec succès.';
		END
	END
END TRY
BEGIN CATCH
	SET @messageRetour = 'Erreur base de données : ' + ERROR_MESSAGE() ;
	SET @codeRetour = 4 ;
END CATCH

RETURN @codeRetour;
GO

--===============================================================================
-- Procedure qui permet de renommer un modèle dans la base
-- @idModeleARenommer est le nom du modèle à renommer
-- @nouveauNom est le nouveau nom à affecter au modèle
-- @messageRetour est le message retour
-- Code retour :
--			0. OK
--			1. Paramètre d'entrée à valeur nulle ou incohérente
--			2. Identifiant modèle non reconnu
--			3. Nouveau nom déjà existant
--			4. Exception
--===============================================================================
CREATE PROC sp_renommerModele @idModeleARenommer TypeIDModele, @nouveauNom TypeIDModele, @messageRetour TypeMessageRetour output
AS
DECLARE @codeRetour Tinyint;

BEGIN TRY
	BEGIN
		IF @idModeleARenommer IS NULL OR @idModeleARenommer = ''
		BEGIN
			SET @codeRetour = 1 ;
			SET @messageRetour = 'Nom du modèle à renommer nul ou vide.';
		END
		ELSE IF @nouveauNom IS NULL OR @nouveauNom = ''
		BEGIN
			SET @codeRetour = 1 ;
			SET @messageRetour = 'Nouveau nom de modèle nul ou vide.';
		END
		ELSE IF NOT EXISTS (SELECT idModele FROM Modele WHERE idModele = @idModeleARenommer)
		BEGIN
			SET @codeRetour = 2 ;
			SET @messageRetour = 'Le modèle à renommer n''existe pas dans la table.';
		END
		ELSE IF EXISTS (SELECT idModele FROM Modele WHERE idModele = @nouveauNom)
		BEGIN
			SET @codeRetour = 3 ;
			SET @messageRetour = 'Le nouveau nom à affecter au modèle existe déjà dans la table.';
		END
		ELSE
		BEGIN
			UPDATE Modele
			SET idModele = @nouveauNom
			WHERE idModele = @idModeleARenommer ;
			SET @codeRetour = 0 ;
			SET @messageRetour = 'Modèle renommé avec succès.';
		END
	END
END TRY
BEGIN CATCH
	SET @messageRetour = 'Erreur base de données : ' + ERROR_MESSAGE() ;
	SET @codeRetour = 4 ;
END CATCH

RETURN @codeRetour;
GO


--===============================================================================
-- Procedure qui permet changer le statut d'un modèle (obsolète ou non)
-- @idModele est le nom du modèle à updater
-- @statut est le statut à affecter au modèle (0 en service, 1 obsolète)
-- @messageRetour est le message retour
-- Code retour :
--			0. OK
--			1. Paramètre d'entrée à valeur nulle ou incohérente
--			2. Identifiant modèle non reconnu
--			3. Exception
--===============================================================================
CREATE PROC sp_changerStatutModele @idModele TypeIDModele, @statut TINYINT, @messageRetour TypeMessageRetour output
AS
DECLARE @codeRetour Tinyint;

BEGIN TRY
	BEGIN
		IF @idModele IS NULL OR @idModele = ''
		BEGIN
			SET @codeRetour = 1 ;
			SET @messageRetour = 'Nom du modèle à mettre à jour nul ou vide.';
		END
		ELSE IF @statut IS NULL OR (@statut <> 0 AND @statut <> 1)
		BEGIN
			SET @codeRetour = 1 ;
			SET @messageRetour = 'Le statut à affecter est nul ou du mauvais format.';
		END
		ELSE IF NOT EXISTS (SELECT idModele FROM Modele WHERE idModele = @idModele)
		BEGIN
			SET @codeRetour = 2 ;
			SET @messageRetour = 'Le modèle n''existe pas dans la table.';
		END
		ELSE
		BEGIN
			UPDATE Modele
			SET Obsolete = @statut
			WHERE idModele = @idModele ;
			SET @codeRetour = 0 ;
			SET @messageRetour = 'Statut mis à jour avec succès.';
		END
	END
END TRY
BEGIN CATCH
	SET @messageRetour = 'Erreur base de données : ' + ERROR_MESSAGE() ;
	SET @codeRetour = 4 ;
END CATCH

RETURN @codeRetour;
GO
